# SPDX-FileCopyrightText: : 2017-2024 The PyPSA-Eur Authors
# Changes by Adrian Odenweller <adrian.odenweller@pik-potsdam.de>, Johannes Hampp <johannes.hampp@pik-potsdam.de>
#
# SPDX-License-Identifier: MIT

from pathlib import Path
import yaml
from os.path import normpath, exists
from shutil import copyfile, move, rmtree
from snakemake.utils import min_version
import numpy as np
import pandas as pd

min_version("8.11")

from scripts._helpers import path_provider, copy_default_files, get_scenarios, get_rdir

copy_default_files(workflow)

configfile: "config/config.remind.yaml"

run = config["run"]
#scenarios = get_scenarios(run)
#RDIR = get_rdir(run)

shared_resources = run["shared_resources"]["policy"]
exclude_from_shared = run["shared_resources"]["exclude"]
logs = path_provider("logs/", "", shared_resources, exclude_from_shared)
benchmarks = path_provider("benchmarks/", "", shared_resources, exclude_from_shared)
resources = path_provider("resources/", "", shared_resources, exclude_from_shared)

# REMIND coupling specific directories
# Structure, e.g.: resources/{scenario}/i{iteration}/y{year}/
RDIR = "{scenario}/"
IDIR = "i{iteration}/"
CDIR = RDIR if not config.get("run", {}).get("shared_cutouts") else ""

LOGS = "logs/"
SCENARIO_LOGS = LOGS + RDIR
ITERATION_LOGS = SCENARIO_LOGS + IDIR

BENCHMARKS = "benchmarks/"
SCENARIO_BENCHMARKS = BENCHMARKS + RDIR
ITERATION_BENCHMARKS = SCENARIO_BENCHMARKS + IDIR

RESOURCES = "resources/" + (
    "" if shared_resources else RDIR
)
SCENARIO_RESOURCES = "resources/" + RDIR
ITERATION_RESOURCES = SCENARIO_RESOURCES + IDIR

RESULTS = "results/"
SCENARIO_RESULTS = RESULTS + RDIR
ITERATION_RESULTS = SCENARIO_RESULTS + IDIR

# Rules that are run locally, not via a SLURM job
localrules:
    purge,
    dag,
    sync,
    download_and_prepare,
    solve_all_scenarios,
    extract_coupling_parameters


wildcard_constraints:
    simpl="[a-zA-Z0-9]*",
    clusters="[0-9]+(m|c)?|all",
    ll=r"(v|c)([0-9\.]+|opt)",
    opts=r"[-+a-zA-Z0-9\.]*",
    year="2[01][0-9][05]",
    iteration="[0-9]+",


include: "rules/common.smk"
include: "rules/retrieve.smk"
include: "rules/build_electricity.smk"
include: "rules/solve_electricity.smk"
include: "rules/postprocess.smk"


rule purge:
    run:
        import builtins

        do_purge = builtins.input(
            "Do you really want to delete all generated resources, \nresults and docs (downloads are kept)? [y/N] "
        )
        if do_purge == "y":
            rmtree("resources/", ignore_errors=True)
            rmtree("results/", ignore_errors=True)
            rmtree("doc/_build", ignore_errors=True)
            print("Purging generated resources, results and docs. Downloads are kept.")
        else:
            raise Exception(f"Input {do_purge}. Aborting purge.")


rule dag:
    message:
        "Creating DAG of workflow."
    output:
        dot=resources("dag.dot"),
        pdf=resources("dag.pdf"),
        png=resources("dag.png"),
    conda:
        "envs/environment.yaml"
    shell:
        r"""
        snakemake --rulegraph all | sed -n "/digraph/,\$p" > {output.dot}
        dot -Tpdf -o {output.pdf} {output.dot}
        dot -Tpng -o {output.png} {output.dot}
        """


rule doc:
    message:
        "Build documentation."
    output:
        directory("doc/_build"),
    shell:
        "make -C doc html"


rule sync:
    params:
        cluster=f"{config['remote']['ssh']}:{config['remote']['path']}",
    shell:
        """
        rsync -uvarh --ignore-missing-args --files-from=.sync-send . {params.cluster}
        rsync -uvarh --no-g {params.cluster}/resources . || echo "No resources directory, skipping rsync"
        rsync -uvarh --no-g {params.cluster}/results . || echo "No results directory, skipping rsync"
        rsync -uvarh --no-g {params.cluster}/logs . || echo "No logs directory, skipping rsync"
        """


# Helper function to evaluate input functions in the config file.
def evaluate_inputs(input_dict):
    evaluated = {}
    for k, v in input_dict.items():
        if callable(v):
            # Set dummy wild card so that update_config_from_wildcards runs without errors
            evaluated[k] = v({"iteration": 1, "year": 2025, "scenario": "default"})
        else:
            evaluated[k] = v
    return evaluated

# Rule which downloads and prepares all files which will not change
# during REMIND <-> PyPSA-EUR coupled iterations and repeating runs.
# * Use for downloading on e.g. login node with internet connection
# * will create some load ~30m - 1h for creating renewable profiles
# run with:
# > snakemake -call -s Snakefile_remind download_and_prepare
rule download_and_prepare:
    message:
        "Downloading and preparing all single-execution rules. "
        "Running this rule requires an internet connection for downloading "
        "datafiles for the model."
    input:
        # Retrieve cost data
        expand(
            "resources/costs_{year}.csv",
            year=[2020, 2025, 2030, 2035, 2040, 2045, 2050],
        ),
        # Retrieve cutouts and create profiles
        expand(
            "resources/profile_{technology}.nc",
            technology=["onwind", "offwind-ac", "offwind-dc", "solar", "hydro"],
        ),
        # Retrieve electricity demand data
        rules.build_electricity_demand.output[0],
        # Retrieve databundle for rule add_electricity
        **{
            k: v
            for k, v in evaluate_inputs(rules.add_electricity.input).items()
            if "{scenario}" not in v
        }

# # This part contains all rules, which get data from REMIND between iterations.
# # PyPSA-EUR networks are solved between iterations via snakemake calls
# #
# # Some of the new rules have additional wildcards:
# # * {year}: Indicating the REMIND year for which the model is built
# # * {iteration}: Indicating the REMIND iteration for which the model is built

# Build cost data compatible with the original PyPSA-EUR cost data structure from technology-data
# as part of the coupling with REMIND.
# Since the cost data is used in multiple rules, the cost data is provided as a dedicated file
# and not only overwritten in the network as part of the "remind_overwrite_elec_network" rule below.
rule import_REMIND_costs:
    input:
        # no TD data for < 2020 and > 2050
        original_costs=lambda w: f"resources/costs_{np.clip(int(w['year']), a_min=2020, a_max=2050)}.csv",
        original_costs_2025="resources/costs_2025.csv",
        original_costs_2030="resources/costs_2030.csv",
        region_mapping="config/regionmapping_21_EU11.csv",
        technology_cost_mapping="config/technology_cost_mapping.csv",
        remind_data=SCENARIO_RESOURCES + "i{iteration}/REMIND2PyPSAEUR.gdx",
    output:
        costs=RESOURCES + "{scenario}/i{iteration}/y{year}/costs.csv",
    log:
        LOGS + "{scenario}/i{iteration}/y{year}/import_REMIND_costs.log",
    benchmark:
        BENCHMARKS + "{scenario}/i{iteration}/y{year}/import_REMIND_costs"
    group:
        "iy"
    script:
        "scripts/import_REMIND_costs.py"


# Build load data time-series based on REMIND annual load per region and default PyPSA-EUR load profiles.
rule import_REMIND_load:
    input:
        load_timeseries=RESOURCES + "electricity_demand.csv",
        region_mapping="config/regionmapping_21_EU11.csv",
        remind_data=SCENARIO_RESOURCES + "i{iteration}/REMIND2PyPSAEUR.gdx",
    output:
        load_timeseries=SCENARIO_RESOURCES + "i{iteration}/y{year}/electricity_demand.csv",
    log:
        LOGS + "{scenario}/i{iteration}/y{year}/import_REMIND_load.log",
    benchmark:
        BENCHMARKS + "{scenario}/i{iteration}/y{year}/import_REMIND_load"
    group:
        "iy"
    script:
        "scripts/import_REMIND_load.py"


# Build csv with min capacities for PyPSA-EUR based on installed capacities from REMIND.
# Used as RCL (Region-Carrier-Limits) contraints in PyPSA-EUR.
rule import_REMIND_RCL_p_nom_limits:
    input:
        remind_data=SCENARIO_RESOURCES + "i{iteration}/REMIND2PyPSAEUR.gdx",
        region_mapping="config/regionmapping_21_EU11.csv",
        technology_cost_mapping="config/technology_cost_mapping.csv",
    output:
        RCL_p_nom_limits=SCENARIO_RESOURCES
        + "i{iteration}/y{year}/RCL_p_nom_limits.csv",
    log:
        LOGS + "{scenario}/i{iteration}/y{year}/import_REMIND_RCL_p_nom_limits.log",
    benchmark:
        BENCHMARKS + "{scenario}/i{iteration}/y{year}/import_REMIND_RCL_p_nom_limits"
    group:
        "iy"
    script:
        "scripts/import_REMIND_RCL_p_nom_limits.py"


# CO2 price is integrated into model either via config.yaml or via a wildcard
# we do not want to modify the config.yaml, so we use an approach to pass the CO2 price via a wildcard.
# For this we make use of the conditional execution from snakemake via checkpoints (see https://snakemake.readthedocs.io/en/stable/snakefiles/rules.html#data-dependent-conditional-execution), were first the checkpoint is executed
# and then we construct based on the checkpoint results the actual rule call and its input which will then solve the networks
# with their respective CO2 prices:
# 1. checkpoint determine_co2_price_scenarios: extract CO2 prices from REMIND + combine with scenario from config.yaml
# 2. rule solve_all_scenarios: Use the output of the checkpoint to solve all scenarios with specific CO2 prices
checkpoint determine_co2_price_scenarios:
    input:
        region_mapping="config/regionmapping_21_EU11.csv",
        remind_data=SCENARIO_RESOURCES + "i{iteration}/REMIND2PyPSAEUR.gdx",
    output:
        co2_price_scenarios=RESULTS + "{scenario}/i{iteration}/co2_price_scenarios.csv",
    log:
        LOGS + "{scenario}/i{iteration}/determine_co2_price_scenarios.log",
    benchmark:
        BENCHMARKS + "{scenario}/i{iteration}/determine_co2_price_scenarios"
    localrule: True  # Checkpoints cannot be declared "local" with "localrules" statement on top, needs individual declaration as local
    script:
        "scripts/determine_CO2_price_scenarios.py"


# snakemake -n -call -s Snakefile_remind solve_all_scenarios --config remind_scenario=no_scenario iteration=
rule solve_all_scenarios:
    input:
        networks=lambda wildcards: expand(
            RESULTS
            + "{scenario}/i{iteration}/y{year}/networks/elec_s{simpl}_{clusters}_ec_l{ll}_{opts}.nc",
            zip,
            **pd.read_csv(
                checkpoints.determine_co2_price_scenarios.get(
                    scenario=wildcards["scenario"],
                    iteration=wildcards["iteration"],
                ).output["co2_price_scenarios"],
                na_filter=False,
            ).to_dict(orient="list")
        ),


# Extract coupling parameters from PyPSA-EUR runs for hand-over to next iteration of REMIND-EU
rule extract_coupling_parameters:
    input:
        networks=rules.solve_all_scenarios.input["networks"],
        region_mapping="config/regionmapping_21_EU11.csv",
        technology_cost_mapping="config/technology_cost_mapping.csv",
        remind_weights=ITERATION_RESOURCES + "REMIND2PyPSAEUR.gdx",
    output:
        capacity_factors=ITERATION_RESULTS + "validation/capacity_factors.csv",
        capacity_factors_links=ITERATION_RESULTS + "validation/capacity_factors_links.csv",
        availability_factors=ITERATION_RESULTS + "validation/availability_factors.csv",
        curtailments=ITERATION_RESULTS + "validation/curtailments.csv",
        generation_shares=ITERATION_RESULTS + "validation/generation_shares.csv",
        preinstalled_capacities=ITERATION_RESULTS + "validation/preinstalled_capacities.csv",
        peak_residual_loads=ITERATION_RESULTS + "validation/peak_residual_loads.csv",
        grid_capacities=ITERATION_RESULTS + "validation/grid_capacities.csv",
        grid_investments=ITERATION_RESULTS + "validation/grid_investments.csv",
        grid_losses=ITERATION_RESULTS + "validation/grid_losses.csv",
        market_values=ITERATION_RESULTS + "validation/market_values.csv",
        hourly_prices=ITERATION_RESULTS + "validation/hourly_prices.csv",
        load_prices=ITERATION_RESULTS + "validation/load_prices.csv",
        markups=ITERATION_RESULTS + "validation/markups.csv",
        electricity_prices_electrolysis=ITERATION_RESULTS + "validation/electricity_prices_electrolysis.csv",
        loads=ITERATION_RESULTS + "validation/loads.csv",
        energy_balances=ITERATION_RESULTS + "validation/energy_balances.csv",
        potentials=ITERATION_RESULTS + "validation/potentials.csv",
        optimal_capacities=ITERATION_RESULTS + "validation/optimal_capacities.csv",
        h2turb_storages=ITERATION_RESULTS + "validation/h2turb_storages.csv",
        crossborder_flows=ITERATION_RESULTS + "validation/crossborder_flows.csv",
        crossborder_prices_import=ITERATION_RESULTS + "validation/crossborder_prices_import.csv",
        crossborder_prices_export=ITERATION_RESULTS + "validation/crossborder_prices_export.csv",
        generation_region_shares=ITERATION_RESULTS + "validation/generation_region_shares.csv",
        technology_mapping=ITERATION_RESULTS + "validation/technology_mapping.csv",
        gdx=ITERATION_RESULTS + "PyPSAEUR2REMIND.gdx",
    log:
        LOGS + "{scenario}/i{iteration}/extract_coupling_parameters.log",
    benchmark:
        BENCHMARKS + "{scenario}/i{iteration}/extract_coupling_parameters/"
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 10000,
        walltime="00:10:00",
    script:
        "scripts/extract_coupling_parameters.py"
